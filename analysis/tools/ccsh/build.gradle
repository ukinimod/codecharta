buildscript {
    ext.kotlin_version = '1.2.21'

    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'gradle.plugin.edu.sc.seis.gradle:launch4j:2.4.3'
    }
}

group 'de.maibornwolff.codecharta'
version '1.6.5'

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'edu.sc.seis.launch4j'

mainClassName = 'de.maibornwolff.codecharta.tools.ccsh.Ccsh'
applicationName = 'ccsh'

repositories {
    mavenCentral()
}

launch4j {
    mainClassName = 'de.maibornwolff.codecharta.tools.ccsh.Ccsh'
}

dependencies {
    compile project(':import:CSVImporter')
    compile project(':import:SCMLogParser')
    compile project(':import:SCMLogParser')
    compile project(':import:SonarImporter')
    compile project(':filter:MergeFilter')
    compile project(':tools:ValidationTool')

    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    compile 'info.picocli:picocli:2.2.1'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    baseName = "${applicationName}"
    manifest {
        attributes 'Main-Class': mainClassName,
                'Implementation-Title': 'CodeCharta ccsh',
                'Implementation-Version': version
    }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}