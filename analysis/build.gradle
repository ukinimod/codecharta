plugins {
    id 'org.sonarqube' version '2.6.1'
    id 'distribution'
    id 'maven'
}

group 'de.maibornwolff.codecharta'
version = currentVersion

repositories {
    mavenCentral()
}

allprojects {
    group 'de.maibornwolff.codecharta'
    version = currentVersion

    repositories.addAll(rootProject.getBuildscript().getRepositories())

    apply plugin: 'jacoco'
    apply plugin: 'maven'

    jacoco {
        toolVersion = "0.7.8"
    }
}

distributions {
    main {
        baseName = 'codecharta-analysis'
        contents {
            from { '../LICENSE.md' }
            from { '../CHANGELOG.md' }

            // Readmes
            from { 'README.md' }

            project.subprojects.each { p ->
                p.plugins.withType(ApplicationPlugin) {
                    into('bin') {
                        from { p.startScripts.outputs.files }
                        fileMode = 0755
                    }
                    into('lib') {
                        from { p.jar }
                    }
                }
            }

            // deprecated ccsh
            from {'dist/ccsh'}
        }
    }
}

sonarqube {
    properties {
        property 'sonar.coverage.exclusions', '**/model/**, **/*Main*'
    }
}

task integrationTest(type: Exec) {
    executable 'sh'
    workingDir('test')
    args '-c', "./golden_test.sh $version"
}

integrationTest.dependsOn build
